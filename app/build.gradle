apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.harrison.foodonmymind"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

//    below is needed for the installRelease gradle tasks to show up for each flavor
//    see https://developer.android.com/studio/publish/app-signing.html#secure-key
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
//    need to add the signingConfig signingConfigs.release to then get the install release task
//    to appear
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        free {
            applicationId "com.harrison.foodonmymind.free"
        }
        paid {
            applicationId "com.harrison.foodonmymind.paid"
        }
    }
}



dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //    needed to add the below dependency for TabLayouts to work
    //    required to use the FusedLocationProviderClient class
    //    adding the below so that I can use the FragmentCompat library for the onResultsCallBack
//    interface for when we request permissions for GPS location
//    note we need the 'com.android.support:support-v13:<the version of the library that we want
//    to include...ie see above we wanted the 25.3.1 version of this library>
    //    technically don't need google player services ads i think because to serve ads we use
//    firebase ads dependency
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.android.support:design:25.3.1'
    compile 'com.google.android.gms:play-services-location:11.0.1'
    compile 'com.android.support:support-v13:25.3.1'
    testCompile 'junit:junit:4.12'
    freeCompile 'com.google.android.gms:play-services-ads:11.0.1'
    freeCompile 'com.google.firebase:firebase-ads:11.0.1'
}
